plugins {
	id 'jqwik.common-configuration'
}

static isRunningInCI() {
	return System.getenv("CI") == "true"
}

description = "Jqwik modular API integration test"

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

def localJavaTarget = "${javaTargetVersion}" == "1.8" ? JavaVersion.VERSION_1_9
	: JavaVersion.toVersion("${javaTargetVersion}")

java {
	sourceCompatibility = localJavaTarget
	targetCompatibility = localJavaTarget

	modularity.inferModulePath = true
}

test {
	useJUnitPlatform()

	testLogging.showStandardStreams = showStandardStreams
}

dependencies {
	if (isRunningInCI()) {
		testImplementation(project(":api"))
		testImplementation(project(":web"))
		testRuntimeOnly(project(":engine"))
	} else {
		testImplementation("net.jqwik:jqwik-api:1.8.2")
		testImplementation("net.jqwik:jqwik-web:1.8.2")
		testRuntimeOnly("net.jqwik:jqwik-engine:1.8.2")
	}
}

tasks.configureEach {
	onlyIf { JavaVersion.current().java9Compatible }
}
